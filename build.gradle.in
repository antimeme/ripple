// Gradle configuration for Ripple                  -*- mode: groovy -*-
plugins {
  id "java-library"
  id "war"
}
description = "Creates a WAR file with servlets and applications"
version = "@VERSION@"
repositories { mavenCentral() }
sourceSets {
    application {
        java {
            srcDirs = ["source"]
            exclude "servlet"
        }
    }
    servlet { java { srcDirs = ["source/servlet"] } }
}

dependencies {
    applicationRuntime "com.googlecode.soundlibs:vorbisspi:1.0.3.3"
    //applicationRuntime "dev.mccue:vorbisspi:0.0.7"

    //servletCompileOnly("jakarta.platform:jakarta.jakartaee-web-api:8.0.0")
    servletCompileOnly "javax.servlet:javax.servlet-api:4.0.1"
    servletRuntime("org.xerial:sqlite-jdbc:3.36.0.1")
    servletCompile(
        "com.fasterxml.jackson.core:jackson-core:2.13.0")
    servletCompile(
        "com.fasterxml.jackson.core:jackson-databind:2.13.0")
    servletCompile(
        "com.fasterxml.jackson.core:jackson-annotations:2.13.0")
}

task generateClassList {
    def sourceSet = sourceSets.application
    def outputFile = file("$buildDir/.classes")

    doLast {
        outputFile.text = ''
        sourceSet.output.classesDirs.each { dir ->
            dir.eachFileRecurse { classFile ->
                if (classFile.isFile() &&
                    classFile.name.endsWith('.class') &&
                    !classFile.name.contains('$')) {
                    def className = classFile.absolutePath
                        .replaceAll("^$dir", '').replaceAll("^/", '')
                        .replaceAll('\\\\', '/')
                        .replaceAll('\\.class$', '')
                        .replaceAll('/', '.')
                    outputFile.append("$className\n")
                }
            }
        }
    }
}

jar {
    archiveName = "@PACKAGE@-@VERSION@.jar"
    generateClassList
    from "$buildDir/.classes"
    from sourceSets.application.output
    from("apps/images") { into "images" }
    from("apps/fonts") { into "fonts" }
    from("apps/sounds") { into "sounds" }
    manifest { from("META-INF/MANIFEST.MF") }
}

war {
    archiveName = "@PACKAGE@-@VERSION@.war"
    classpath(configurations.servletCompile,
              configurations.servletRuntime)
    webXml = file "WEB-INF/web.xml"
    into("WEB-INF") { from("WEB-INF/jetty-env.xml") }
    into("WEB-INF/classes") { from sourceSets.servlet.output }
    generateClassList
    from "$buildDir/.classes"
    from sourceSets.application.output
    from "apps"
    manifest {
        attributes(
            "Main-Class": "net.antimeme.ripple.Ripple",
            "Application-Name": "Ripple",
            "Permissions": "sandbox",
            "Implementation-Title": "net.antimeme.ripple",
            "Implementation-Version": "@VERSION@",
            "Implementation-Vendor": "@PACKAGE@@antimeme.net")
    }
}
